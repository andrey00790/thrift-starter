buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.1.RELEASE'
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.2.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.0'
    }
}


allprojects {

    group = 'com.microservices'
    version = '1.0.0'

    repositories {
        jcenter()
    }

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-starter-parent:Finchley.SR1"
        }

        dependencies {
            dependency 'org.projectlombok:lombok:1.16.8'
            dependency 'org.codehaus.groovy:groovy-all:2.4.5'
            dependency 'org.apache.thrift:libthrift:0.9.3'
            dependency 'org.apache.commons:commons-pool2:2.4.2'
            dependency 'org.apache.commons:commons-lang3:3.5'
        }
    }

    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        compileOnly ('org.springframework.boot:spring-boot-starter')
        compileOnly ("org.jetbrains.kotlin:kotlin-reflect")
        ['armeria',
         'armeria-grpc',
         'armeria-jetty',
         'armeria-kafka',
         'armeria-logback',
         'armeria-retrofit2',
         'armeria-thrift',
         'armeria-tomcat',
         'armeria-zipkin',
         'armeria-zookeeper'].each {
            compile "com.linecorp.armeria:${it}:0.70.0"
        }
        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}
